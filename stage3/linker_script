OUTPUT_FORMAT("elf64-x86-64")

ENTRY(_start)

SECTIONS
{
    . = 0x7f000000;
    START = .;

    .start ALIGN(4096):
    {
        *(.start)
    }

    /* the default linker aligns the file and text without throwing
       away a page..but for today...*/
    text_start = .;
    .text : ALIGN(4096)
    {
        *(.text)
        *(.text.*)
        . = ALIGN(4096);
        vdso_start = .;
        *(.vdso)        /* must be at end of section; we use text_end to determine its extent */
        vdso_end = .;
    }
    text_end = .;

    .rodata : ALIGN(4096)
    {
        *(.rodata)
        *(.rodata.*)
    }

    .data : ALIGN(4096)
    {
        *(.data)
        *(.data.*)
    }

    PROVIDE(bss_start = .);
    .bss  ALIGN(32):
    {
        *(.bss)
        *(.bss.*)
        *(COMMON)
    }
    PROVIDE(bss_end = .);
}
