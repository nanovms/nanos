OUTPUT_FORMAT("elf64-x86-64")

ENTRY(_phys_start)

INCLUDE klib-syms.lds

PHDRS
{
    text PT_LOAD FLAGS(5);          /* R E */
    rodata PT_LOAD FLAGS(4);        /* R */
    data PT_LOAD FLAGS(6);          /* RW */
    stack PT_GNU_STACK FLAGS(7);    /* RWE */
    note PT_NOTE FLAGS(4);          /* R */
}

/* Note these are ordered such that sections are grouped together by write
 * permission, delimited by the READONLY_END symbol */
SECTIONS
{
    . = 0;
    AP_BOOT_PAGE = .;

    /* Physical addresses are only relevant in case of direct stage3 load. */
    LOAD_OFFSET = START - 0x200000;     /* 0x200000 equals KERNEL_BASE_PHYS */
    _phys_start = _start - LOAD_OFFSET; /* entry point physical adddress */
    _phys_pvh_start32 = pvh_start32 - LOAD_OFFSET;

    . = 0xffffffff80000000;
    START = .;

    .start ALIGN(4096): AT(ADDR(.start) - LOAD_OFFSET)
    {
        *(.start)
    }

    /* the default linker aligns the file and text without throwing
       away a page..but for today...*/
    text_start = .;
    .text ALIGN(4096): AT(ADDR(.text) - LOAD_OFFSET)
    {
        *(.text)
        *(.text.*)
        . = ALIGN(4096);
        vsyscall_start = .;
        *(.vsyscall)        /* must be at end of section; we use text_end to determine its extent */
        vsyscall_end = .;
        . = ALIGN(4096);    /* pad out the page so that rodata will be aligned */
    } :text
    text_end = .;

    .rodata ALIGN(4096): AT(ADDR(.rodata) - LOAD_OFFSET)
    {
        *(.rodata)
        *(.rodata.*)
    } :rodata

    READONLY_END = .;

    .data ALIGN(4096): AT(ADDR(.data) - LOAD_OFFSET)
    {
        ro_after_init_start = .;
        *(.ro_after_init)
        . = ALIGN(4096);
        ro_after_init_end = .;
        *(.data)
        *(.data.*)
    } :data

    PROVIDE(bss_start = .);
    .bss  ALIGN(4096): AT(ADDR(.bss) - LOAD_OFFSET)
    {
        bss_ro_after_init_start = .;
        *(.bss.ro_after_init)
        . = ALIGN(4096);
        bss_ro_after_init_end = .;
        *(.bss)
        *(.bss.*)
        *(COMMON)
    } :data
    PROVIDE(bss_end = .);

    /* All kernel variables accessible to userspace
     * Offsets from vvar_page must match the corresponding offsets specified
     * in src/x86_64/vdso.lds
     *
     * We could look at generating them automatically, but for now it's easy enough
     * to do it this way
     */
    .vvar ALIGN(4096): AT(ADDR(.vvar) - LOAD_OFFSET)
    {
        vvar_page = .;
        __vdso_vdso_dat = vvar_page + 128;
        . += 4096; /* reserve a page since the linker seems to discard this section */
    } :data

    END = .;

    .note :
    {
        KEEP(*(.note.*))
    } :note
}
