OUTPUT_FORMAT("elf64-x86-64")

ENTRY(_phys_start)

SECTIONS
{
    . = 0;
    AP_BOOT_PAGE = .;

    /* Physical addresses are only relevant in case of direct stage3 load. */
    LOAD_OFFSET = START - 0x200000;     /* 0x200000 equals KERNEL_BASE_PHYS */
    _phys_start = _start - LOAD_OFFSET; /* entry point physical adddress */

    . = 0xffffffff80000000;
    START = .;

    .start ALIGN(4096): AT(ADDR(.start) - LOAD_OFFSET)
    {
        *(.start)
    }

    /* the default linker aligns the file and text without throwing
       away a page..but for today...*/
    text_start = .;
    .text ALIGN(4096): AT(ADDR(.text) - LOAD_OFFSET)
    {
        *(.text)
        *(.text.*)
        . = ALIGN(4096);
        vsyscall_start = .;
        *(.vsyscall)        /* must be at end of section; we use text_end to determine its extent */
        vsyscall_end = .;
    }
    text_end = .;

    /* All kernel variables accessible to userspace
     * Offsets from vvar_page must match the corresponding offsets specified
     * in src/x86_64/vdso.lds
     *
     * We could look at generating them automatically, but for now it's easy enough
     * to do it this way
     */
    .vvar ALIGN(4096): AT(ADDR(.vvar) - LOAD_OFFSET)
    {
        vvar_page = .; 
        __vdso_vdso_dat = vvar_page + 128;
    }

    .rodata ALIGN(4096): AT(ADDR(.rodata) - LOAD_OFFSET)
    {
        *(.rodata)
        *(.rodata.*)
    }

    .data ALIGN(4096): AT(ADDR(.data) - LOAD_OFFSET)
    {
        *(.data)
        *(.data.*)
    }

    PROVIDE(bss_start = .);
    .bss  ALIGN(32): AT(ADDR(.bss) - LOAD_OFFSET)
    {
        *(.bss)
        *(.bss.*)
        *(COMMON)
    }
    PROVIDE(bss_end = .);
    END = .;
}
